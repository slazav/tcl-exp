#!/usr/bin/tclsh

package require DeviceRole 1.1
package require xBlt

## script for measuring amplifier gain as a function of input amplitude
## with a generator, power supply and a lock-in amp.

## set all parameters from cmdline
#
# -g -gen_dev   -- generator device (default gen0:1)
# -m -meas_dev  -- measurement device (default lockin0:XY)
# -p -ps_dev    -- power supply device (default gen1)
# -o -offs      -- offset (default 0.0)
# -F -freq      -- frequency (default 1e6)
# -V -vcc       -- Vcc (default 4)
# -A -amin      -- min amplitude (default 0.002)
# -B -amax      -- max amplitude (default 2)
# -n            -- number of points (default 21)
# -L -log       -- logarithmic step (default 0)
# -d -dt        -- delay between samples  (default 0.5)
# -D -del       -- initial delay (default 2)
# -r -range     -- measurement range, V (default 0.1)
# -t -tconst    -- measurement time constant, s (default 0.1)
# -g -gfactor   -- gain factor, ratio of measured/set signal for direct connecton (default 1)

set options [list \
{-g -gen_dev}  gen_dev  {gen0:1}\
{-m -meas_dev} meas_dev {lockin0:XY}\
{-p -ps_dev}   ps_dev   {gen1}\
{-o -offs}     offs     {0.0}\
{-F -freq}     freq     {1e6}\
{-V -vcc}      vcc      {3}\
{-A -amin}     amin     {0.002}\
{-B -amax}     amax     {2}\
{-G -fmax}     fmax     {2e6}\
{-n}           npts     {21}\
{-L}           log      {0}\
{-d -dt}       dt       {0.5}\
{-D -del}      del      {2}\
{-r -range}    range    {0.1}\
{-t -tconst}   tconst   {0.1}\
{-g -gfactor}  gfact    {1}\
]

xblt::parse_options "get_gain_vcc" $argv $options


if {$npts <= 0} {error "$npts <= 0"}

if {$npts!=1} {
  if {$amax <= $amin} {error "$amax <= $amin"}
  set dv  [expr {1.0*($amax-$amin)/($npts-1)}]
  set dvk [expr {pow(1.0*$amax/$amin, 1.0/($npts-1))}]
} else {
  set dv 0
  set dvk 1
}

## prepare generator
set gen   [DeviceRole $gen_dev ac_source]
$gen set_ac $freq $amin $offs

## prepare lock-in
set gauge [DeviceRole $meas_dev gauge]
$gauge set_range  $range
$gauge set_tconst $tconst

# prepare power supply
set ps [DeviceRole $ps_dev dc_source]
$ps set_volt $vcc

# initial delay
after [expr {int($del*1000)}]

puts "# freq amp vcc offs gain x y"
set amp $amin
for {set i 0} {$i < $npts} {incr i} {

  set amp [format "%.3f" $amp]
  $gen set_ac $freq $amp $offs
  after [expr {int($dt*1000)}]
  set out [$gauge get]
  set x [lindex $out 0]
  set y [lindex $out 1]
  set g [expr {sqrt($x**2+$y**2)/$gfact/$amp}]
  puts "$freq $amp $vcc $offs $g $x $y"
  set amp [expr {$log? $amp*$dvk : $amp+$dv}]
}


